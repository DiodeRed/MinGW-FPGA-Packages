# Maintainer: DiodeRed

basename=migen
_realname=${basename}-git
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
_ver_base=0.0
pkgver=0.0.1886.9da60e3
pkgrel=1
pkgdesc="A Python toolbox for building complex digital hardware"
arch=('any')
url="https://m-labs.hk/migen"
license=('BSD')
provides=("${MINGW_PACKAGE_PREFIX}-migen")
conflicts=("${MINGW_PACKAGE_PREFIX}-migen")
depends=(
	"${MINGW_PACKAGE_PREFIX}-python3"
	"${MINGW_PACKAGE_PREFIX}-python3-sphinx"
	"${MINGW_PACKAGE_PREFIX}-python3-sphinx_rtd_theme"
	"${MINGW_PACKAGE_PREFIX}-python3-colorama"
	)
makedepends=("git")
source=("$basename"::"git+https://github.com/m-labs/$basename#branch=master")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${basename}"
  printf "%s.%s.%s" "$_ver_base" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "${srcdir}/${basename}"
  rm -rf ${pkgdir}${MINGW_PREFIX}
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} --root="${pkgdir}" --optimize=1 --skip-build
}
